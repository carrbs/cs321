--
Benjamin Carr
Oct 25 2011
CS321 - Prof. Li
(miniLLG.txt)
--

Program    -> ClassDecl {ClassDecl}
ClassDecl  -> "class" <ID> ["extends" <ID>] "{" {VarDecl} {MethodDecl} "}"
PublicMethodDecl -> "public" MethodDecl
MethodDecl -> Type <ID> "(" [Formals] ")" MethodBody
           |  "void" <ID> "(" [Formals] ")" MethodBody
           |  "static" "void" "main" "(" "String" "[" "]" <ID> ")"
                       MethodBody
MethodBody -> "{" {VarDecl} {Statement} "}"
Formals    -> Type <ID> {"," Type <ID>}
VarDecl    -> Type <ID> ["=" InitExpr] ";"
Type       -> BasicType ["[" "]"] | <ID>
BasicType  -> "int" | "float" | "boolean"
Statement  -> "{" {Statement} "}"
           |  Lvalue LvalueStatement
           |  "if" "(" Expr ")" Statement ["else" Statement]
           |  "while" "(" Expr ")" Statement
           |  "System.out.println" "(" [Expr | <STRVAL>] ")" ";"
           |  "return" [Expr] ";"
LvalueStatement      -> "=" InitExpr ";"
                     |  "(" [Args] ")" ";"
Args       -> Expr {"," Expr}
InitExpr   -> "new" NewIntExpr
           |  Expr
NewInitExpr  -> BasicType "[" <INTVAL> "]"
             |  <ID> "(" [Args] ")"
Expr         -> AndExpr OrTail
OrTail       -> [ "||"  AndExpr OrTail ]
AndExpr    -> RelExpr AndTail
AndTail()    -> [ "&&" RelExpr AndTail ]
RelExpr      -> ArithExpr RelTail
RelTail      -> [ Relop ArithExpr ]
ArithExpr -> Term ArithTail
ArithTail    -> [ PlusMinusOp Term ArithTail ]
PlusMinusOp      -> "+" | "-"
Term         -> Factor TermTail
TermTail -> [ MultDivideOp Factor TermTail ]
MultDivideOp     -> "*" | "/"
Factor     -> Unop Factor
           |  "(" Expr ")"
           |  Lvalue [ LvalueFactor ]
           |  Literal
LvalueFactor    -> "(" [Args] ")"
                 |  "." "length" "(" ")"
Lvalue     -> ["this" "."] <ID> {"." <ID>} ["[" Expr "]"]
Literal    -> <INTVAL> | <FLOATVAL> | "true" | "false"
Relop    -> "==" | "!=" | "<" | "<=" | ">" | ">="
Unop       -> "-" | "!"
